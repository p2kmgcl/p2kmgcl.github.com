---
title: Random fly movement
html_compiler: none
css_compiler: sass
js_compiler: coffeescript
---

<canvas class="canvas"></canvas>

- - -

.canvas
    position: absolute
    top: 0
    left: 0
    width: 100%
    height: 100%
    background-color: #fafafa

- - -

flyMovement = (deltatime) ->
    randomX = Math.random() * @speed - @halfSpeed
    randomY = Math.random() * @speed - @halfSpeed
    @velocity.x = (@velocity.x + randomX) / 2
    @velocity.y = (@velocity.y + randomY) / 2
    @position = @position.sum @velocity

# From here there is nothing important

MIN_DELTA = 0.016666667
canvas = SANDBOX_WRAPPER.querySelector '.canvas'
context = canvas.getContext '2d'
mainLoopLast = (new Date()).getTime()

GRAVITY = 9.81

class Vector
    constructor: (@x = 0, @y = 0, @z = 0) ->

    set: (vector) ->
        @x = vector.x
        @y = vector.y
        @z = vector.z

    sum: (vector) ->
        new Vector(
            @x + vector.x
            @y + vector.y
            @z + vector.z
        )

    substract: (vector) ->
        new Vector(
            @x - vector.x,
            @y - vector.y,
            @z - vector.z
        )

    multiply: (escalar) ->
        new Vector(
            @x * escalar,
            @y * escalar,
            @z * escalar
        )

    distance: (vector) ->
        Math.sqrt(
            Math.pow(@x - vector.x, 2) +
            Math.pow(@y - vector.y, 2) +
            Math.pow(@z - vector.z, 2)
        )

class Thing
    constructor: () ->
        @position = new Vector
        @velocity = new Vector
        @acceleration = new Vector

    update: (deltatime) ->
        @acceleration.y += GRAVITY * deltatime
        @velocity = @velocity.sum @acceleration.multiply deltatime
        @position = @position.sum @velocity

    render: (context) ->
        context.save()
        context.strokeStyle = 'blue'
        context.beginPath()
        context.moveTo @position.x, @position.y
        context.lineTo @position.x + @velocity.x * 100,
            @position.y + @velocity.y * 100
        context.closePath()
        context.stroke()
        context.strokeStyle = 'red'
        context.beginPath()
        context.moveTo @position.x, @position.y
        context.lineTo @position.x + @acceleration.x * 100,
            @position.y + @acceleration.y * 100
        context.closePath()
        context.stroke()
        context.restore()

class Fly extends Thing
    constructor: (@size = 1, @speed = 20) ->
        super()
        @hue = parseInt Math.random() * 255, 10
        @halfSpeed = @speed / 2
        @resetPosition()

    resetPosition: () ->
        @position.x = canvas.width * 0.5
        @position.y = canvas.height * 0.4

    update: flyMovement
    render: (context) ->
        context.save()
        context.fillStyle = "hsla(#{@hue}, 100%, 50%, 0.5)"
        context.beginPath()
        context.moveTo @position.x + @size, @position.y
        context.arc @position.x, @position.y, @size, 0, Math.PI * 2, true
        context.closePath()
        context.fill()
        context.restore()
        if @hue is 255 then @hue = 0 else @hue++

flies = []
flies.push new Fly((parseInt Math.random() * 10 + 1, 10),
    (parseInt Math.random() * 25 + 25, 10)) for i in [1..100]

update = (deltatime) ->
    fly.update(deltatime) for fly in flies

render = (context) ->
    fly.render(context) for fly in flies

autoResizeCanvas = ->
    canvas.width = SANDBOX_WRAPPER.offsetWidth
    canvas.height = SANDBOX_WRAPPER.offsetHeight
    fly.resetPosition() for fly in flies
window.onresize = autoResizeCanvas
autoResizeCanvas()

mainLoop = ->
    requestAnimationFrame mainLoop
    mainLoopNow = (new Date()).getTime()
    mainLoopDelta = (mainLoopLast - mainLoopNow) / 1000
    update if mainLoopDelta < MIN_DELTA then MIN_DELTA else mainLoopDelta
    mainLoopLast = mainLoopNow
    render(context)
mainLoop()
